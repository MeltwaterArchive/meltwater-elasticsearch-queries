import static java.lang.System.getenv

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'com.meltwater'
version = project_version
sourceCompatibility = 1.7

repositories{
    jcenter()
    mavenLocal()
    if (getenv('MAVEN_URL')!=null) {
        maven {
            credentials {
                username getenv('MAVEN_USER')
                password getenv('MAVEN_PASSWORD')
            }
            url getenv('MAVEN_URL')
        }
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        JavaArtifacts(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
    repositories {
        maven {
            credentials {
                username getenv('MAVEN_USER')
                password getenv('MAVEN_PASSWORD')
            }
            url getenv('MAVEN_URL')
        }
    }
}

bintray {
    user = getenv('BINTRAY_USER')
    key = getenv('BINTRAY_KEY')
    publications = ['JavaArtifacts']
    publish = true
    pkg {
        repo = 'opensource'
        name = 'meltwater-elasticsearch-queries'
        userOrg = 'meltwater'
        licenses = ['MIT']
        vcsUrl = "https://github.com/meltwater/${project.name}"
        version {
            gpg{
                sign = true
            }
            released  = new Date()
        }
    }
}


task ('wrapper', type:Wrapper) {
    gradleVersion = '3.1'
}

def LUCENE_VERSION = '5.3.1'

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile "org.apache.lucene:lucene-core:${LUCENE_VERSION}"
    compile "org.apache.lucene:lucene-join:${LUCENE_VERSION}"
    compile "org.apache.lucene:lucene-analyzers-common:${LUCENE_VERSION}"
    compile 'com.google.guava:guava:18.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.codehaus.groovy:groovy-all:2.4'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}
